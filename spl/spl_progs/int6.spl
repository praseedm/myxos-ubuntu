alias physicalSP S0;
physicalSP = ([PTBR + 2 * (SP / 512)] * 512) + (SP % 512);

alias sysCallNo S1;
sysCallNo = [physicalSP - 1];

alias fileName S2;
fileName = [physicalSP - 3];

alias PID S8;
PID = (PTBR - 1024) / 8;

alias currentPCB S9;
currentPCB = READY_LIST + 32 * PID;

alias counter S3;


if (sysCallNo == 9) then

	alias FATIndex S4;
	FATIndex = 0;

	while (FATIndex < 64) do
		if ([FAT + FATIndex * 8] == fileName) then
			break;
		endif;
		FATIndex = FATIndex + 1;
	endwhile;

	if (FATIndex == 64) then
		[physicalSP - 2] = -1;
		ireturn;
	endif;

	alias countCodeBlocksNew S5;
	countCodeBlocksNew = [FAT + FATIndex * 8 + 1];
	countCodeBlocksNew = countCodeBlocksNew / 512;

	if (countCodeBlocksNew > 3) then
		[physicalSP - 2] = -1;
		ireturn;
	endif;

	counter = 0;
	while (counter < 3) do
		if ([PTBR + counter * 2 + 1] == "01" || [PTBR + counter * 2 + 1] == "11") then
			[MEM_LIST + [PTBR + counter * 2]] = [MEM_LIST + [PTBR + counter * 2]] - 1;
		endif;
		if ([PTBR + counter * 2 + 1] == "00" && [PTBR + counter * 2] > 447) then
			[DISK_LIST + [PTBR + counter * 2]] = [DISK_LIST + [PTBR + counter * 2]] - 1;
		endif;
		counter = counter + 1;
	endwhile;

	load(1, [FAT + FATIndex * 8 + 2]);

	counter = 0;
	while (counter < countCodeBlocksNew) do
		[PTBR + counter * 2] = [SCRATCHPAD + counter];
		[PTBR + counter * 2 + 1] = "00";
		counter = counter + 1;
	endwhile;

	while (counter < 3) do
		[PTBR + counter * 2] = -1;
		[PTBR + counter * 2 + 1] = "00";
		counter = counter + 1;
	endwhile;

	[PTBR + 7] = "01";


	counter = 0;
	while (counter < 8) do
		if ([currentPCB + 15 + counter * 2] != -1) then

			[FILE_TABLE + [currentPCB + 15 + counter * 2] * 2 + 1] = [FILE_TABLE + [currentPCB + 15 + counter * 2] * 2 + 1] - 1;
			if ([FILE_TABLE + [currentPCB + 15 + counter * 2] * 2 + 1] == 0) then
				[FILE_TABLE + [currentPCB + 15 + counter * 2] * 2] = -1;
			endif;
			[currentPCB + 15 + counter * 2] = -1;
			[currentPCB + 15 + counter * 2 + 1] = -1;
		endif;
		counter = counter + 1;
	endwhile;

	SP = 3 * 512;

	physicalSP = ([PTBR + 2 * (SP / 512)] * 512) + (SP % 512);
	[physicalSP] = 0;

	ireturn;

endif;

if (sysCallNo == 11) then

	[physicalSP - 2] = (PTBR - 1024) / 8;
	ireturn;

endif;

if (sysCallNo == 12) then

	if ([currentPCB + 31] < 0 || [currentPCB + 31] > 31) then
		[physicalSP - 2] = -1;
		ireturn;
	endif;

	[physicalSP - 2] = [currentPCB + 31];
	ireturn;

endif;
